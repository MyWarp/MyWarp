plugins {
    id "com.palantir.git-version" version "0.12.3"
}

/*
 * Copyright (C) 2011 - 2019, MyWarp team and contributors
 *
 * This file is part of MyWarp.
 *
 * MyWarp is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * MyWarp is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with MyWarp. If not, see <http://www.gnu.org/licenses/>.
 */

// build configuration
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

ext.resolveProperty = {key, defaultValue = "unknown" ->
    return project.findProperty(key) ?: System.getenv(key) ?: defaultValue
}

// define variables
ext.buildNumber = resolveProperty("BUILD_NUMBER")
ext.ciSystem = resolveProperty("CI_SYSTEM")
ext.commitHash = resolveProperty("COMMIT_HASH", versionDetails().gitHash)

allprojects {
    group = "io.github.mywarp.mywarp"
    version = "3.1.1-SNAPSHOT"

    // general project information
    ext.projectName = "MyWarp"
    ext.internalVersion = "${version}+${ciSystem}-b${buildNumber}.git-${commitHash}"
    ext.url = "https://mywarp.github.io/"
    ext.inceptionYear = "2011"
    ext.projectDescription = "An extension for the Minecraft multiplayer that allows players to create and share warps with each other."
}
apply from: "${rootProject.projectDir}/gradle/publishOnGithub.gradle"

subprojects {
    apply plugin: "java"
    apply plugin: "checkstyle"
    apply from: "${rootProject.projectDir}/gradle/i18n.gradle"

    sourceCompatibility = 8
    targetCompatibility = 8

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    jar {
        from new File(rootProject.projectDir, "LICENSE.txt")

        manifest.mainAttributes(
                "Built-By": System.properties["user.name"],
                "Created-By": System.properties["java.vm.version"] + " (" + System.properties["java.vm.vendor"] + ")",
                "Implementation-Title": name,
                "Implementation-Version": internalVersion,
                "Implementation-Vendor": url)
    }

    javadoc {
        options.setEncoding("UTF-8")

        if (JavaVersion.current().isJava8Compatible()) {
            // disable the super-strict doclint tool in Java 8
            options.addStringOption("Xdoclint:none", "-quiet")
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }
}
