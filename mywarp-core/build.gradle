/*
 * Copyright (C) 2011 - 2020, MyWarp team and contributors
 *
 * This file is part of MyWarp.
 *
 * MyWarp is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * MyWarp is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with MyWarp. If not, see <http://www.gnu.org/licenses/>.
 */

import ru.vyarus.gradle.plugin.animalsniffer.signature.BuildSignatureTask

plugins {
    id "ru.vyarus.animalsniffer" version "1.5.2"
    id 'java-library'
}

configurations.create("createSignature")

dependencies {
    implementation "org.jooq:jooq:3.14.3"
    annotationProcessor "org.jooq:jooq-checker:3.14.3"
    implementation "org.flywaydb:flyway-core:7.2.1"
    implementation "com.google.guava:guava:21.0"
    implementation "com.github.mywarp.intake:intake:b57bff7cd9"

    api "org.slf4j:slf4j-api:1.7.30"
    api "com.flowpowered:flow-math:1.0.3"

    signature "org.codehaus.mojo.signature:java18:1.0@signature" //required to build custom signature
    createSignature "com.google.guava:guava:10.0.1" //bundled by Bukkit 1.7.10, we should try to stay compatible
}

// Use JOOQ's annotation processor
compileJava.options.compilerArgs << "-processor" << "org.jooq.checker.SQLDialectChecker"

task sig(type: BuildSignatureTask) {
    //build new signature as an extension to the JDK's
    signatures configurations.signature
    files configurations.createSignature
}
animalsniffer {
    //use the signature generated above
    signatures = sig.outputFiles
    excludeJars "guava-*"
    //the cache cannot be used due to incompatibilities with Java 8,
    //see https://github.com/xvik/gradle-animalsniffer-plugin/wiki/Check-task-performance#known-issues
}


