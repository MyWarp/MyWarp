import ru.vyarus.gradle.plugin.animalsniffer.signature.BuildSignatureTask

plugins {
    id "ru.vyarus.animalsniffer" version "1.5.3"
    id 'java-library'
}

configurations.create("createSignature")

dependencies {
    implementation "org.jooq:jooq:3.14.11"
    annotationProcessor "org.jooq:jooq-checker:3.14.11"
    implementation "org.flywaydb:flyway-core:8.1.0"
    implementation "com.google.guava:guava:21.0"
    implementation "com.github.mywarp.intake:intake:b57bff7cd9"

    api "org.slf4j:slf4j-api:1.7.31"
    api "com.flowpowered:flow-math:1.0.3"

    signature "org.codehaus.mojo.signature:java18:1.0@signature"
    //required to build custom signature
    createSignature "com.google.guava:guava:10.0.1"
    //bundled by Bukkit 1.7.10, we should try to stay compatible
}

// Use JOOQ's annotation processor
compileJava.options.compilerArgs << "-processor" << "org.jooq.checker.SQLDialectChecker"

task sig(type: BuildSignatureTask) {
    //build new signature as an extension to the JDK's
    signatures configurations.signature
    files configurations.createSignature
}
animalsniffer {
    //use the signature generated above
    signatures = sig.outputFiles
    excludeJars "guava-*"
    //the cache cannot be used due to incompatibilities with Java 8,
    //see https://github.com/xvik/gradle-animalsniffer-plugin/wiki/Check-task-performance#known-issues
}


